plugins {
	id 'org.beryx.runtime'
	id 'com.google.osdetector'
}

configurations {
	assembly
}

def dist = tasks.register('distribution', Zip) {
	group = "distribution"
	archiveBaseName = "grafikon"
	archiveVersion = scmVersion.shortVersion
	destinationDirectory = file("${buildDir}/dist")
	from configurations.assembly
	from file("${projectDir}/src/main/deployment/scripts")
	rename "grafikon-start.*", "grafikon.jar"
}

tasks.register('unzippedDistribution', Sync) {
	group = "distribution"
	dependsOn dist
	from zipTree(dist.get().archiveFile)
	into "${buildDir}/unzipped-dist"
}

tasks.register('jpackageImageZip', Zip) {
	group = "distribution"
	archiveBaseName = "grafikon"
	archiveVersion = scmVersion.shortVersion
	archiveClassifier = osdetector.classifier
	destinationDirectory = file("${buildDir}/jpackage-zip")
	dependsOn tasks.named('jpackageImage')
	dependsOn tasks.named('jpackage')
	from file("${buildDir}/jpackage/" + (osdetector.os == "osx" ? "grafikon.app" : "grafikon"))
}

ext {
	gMainClass = 'net.parostroj.timetable.gui.Main'
}

tasks.register('runMain', JavaExec) {
	classpath = sourceSets.main.runtimeClasspath
	mainClass = gMainClass
	group = "application"
	description = "Runs application"
}

dependencies {
	assembly project

	implementation project(':grafikon-gui')
	implementation project(':grafikon-save')
	implementation project(':grafikon-templates')

	runtimeOnly project(':grafikon-ls')
	runtimeOnly project(':grafikon-ls4')

	implementation 'org.apache.logging.log4j:log4j-slf4j2-impl'
	implementation 'org.apache.logging.log4j:log4j-core'
	implementation 'org.glassfish.jaxb:jaxb-runtime'
}

tasks.named('jar').configure {
	manifest {
		attributes(
			'Main-Class': gMainClass,
			'Class-Path': configurations.runtimeClasspath.files.collect { file -> file.name }.join(' '),
			'SplashScreen-Image': 'images/splashscreen.png'
		)
	}
}

application {
	mainClass = gMainClass
	applicationName = 'grafikon'
}

tasks.named('distZip').configure { enabled = false }
tasks.named('distTar').configure { enabled = false }

runtime {
	options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
	modules = ['java.xml', 'java.desktop', 'java.logging', 'java.scripting', 'java.management',
			'jdk.xml.dom', 'java.naming', 'jdk.zipfs']
	imageDir = file("${buildDir}/grafikon-image")
	imageZip = file("${buildDir}/image-zip/grafikon-image-${scmVersion.shortVersion}-${osdetector.classifier}.zip")
	launcher {
		noConsole = true
	}
	jpackage {
		imageName = 'grafikon'
		installerName = 'grafikon'
		def commonOptions = ['--copyright', "(c) ${scmVersion.year} jub", '--vendor', 'www.parostroj.net',
			'--description', 'Train diagram editor']
		installerOptions = commonOptions
		imageOptions = commonOptions
		if (osdetector.os == 'windows') {
			appVersion = project.findProperty('appVersion') ?: scmVersion.baseVersion
			installerOptions += ['--win-dir-chooser', '--win-menu', '--win-menu-group', '',
				'--win-upgrade-uuid', '2318a3ac-b334-4978-90d1-987da0d4379f',
				'--file-associations', 'src/main/deployment/resources/win-associations.properties']
			imageOptions += ['--icon', 'src/main/deployment/icons/grafikon.ico']
			installerType = project.findProperty('installerType') ?: 'msi'
		} else if (osdetector.os == 'linux') {
			appVersion = project.findProperty('appVersion') ?: scmVersion.shortVersion
			installerOptions += ['--linux-shortcut', '--resource-dir', 'src/main/deployment/linux-resources',
				'--file-associations', 'src/main/deployment/resources/linux-associations.properties']
			imageOptions += ['--icon', 'src/main/deployment/icons/grafikon.png']
			installerType = project.findProperty('installerType') ?: 'deb'
		} else if (osdetector.os == "osx") {
			appVersion = project.findProperty('appVersion') ?: scmVersion.baseVersion
			installerOptions += ['--mac-package-name', 'Grafikon',
				'--file-associations', 'src/main/deployment/resources/mac-associations.properties']
			imageOptions += ['--icon', 'src/main/deployment/icons/grafikon.icns']
			installerType = project.findProperty('installerType') ?: 'dmg'
		}
	}
}
