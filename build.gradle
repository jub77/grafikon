plugins {
	id 'io.spring.dependency-management' version '1.0.8.RELEASE' apply false
	id 'net.parostroj.versioning' apply false
	id 'com.github.ben-manes.versions' version '0.22.0'
}

def nonJavaProjectsFilter = [
	"grafikon-assembly",
	"grafikon-webstart"
]

subprojects { prj ->
	apply plugin: 'net.parostroj.versioning'
	apply plugin: 'io.spring.dependency-management'

	group = 'net.parostroj'
	version = scmVersion.projectVersion

	repositories {
		mavenLocal()
		mavenCentral()
		maven { url 'https://jitpack.io' }
	}

	dependencyManagement {
		dependencies {
			dependency 'org.jgrapht:jgrapht-core:1.3.1'
			dependency 'org.mvel:mvel2:2.4.4.Final'
			dependency 'org.codehaus.groovy:groovy-templates:2.5.8'
			dependency 'org.codehaus.groovy:groovy-jsr223:2.5.8'
			dependency 'org.slf4j:slf4j-api:1.7.27'
			dependency 'org.slf4j:jcl-over-slf4j:1.7.27'
			dependency 'joda-time:joda-time:2.10.3'
			dependency 'com.google.guava:guava:28.0-jre', {
				exclude 'com.google.code.findbugs:jsr305'
				exclude 'com.google.j2objc:j2objc-annotations'
				exclude 'com.google.errorprone:error_prone_annotations'
				exclude 'org.codehaus.mojo:animal-sniffer-annotations'
				exclude 'org.checkerframework:checker-compat-qual'
				exclude 'org.checkerframework:checker-qual'
				exclude 'com.google.guava:failureaccess'
				exclude 'com.google.guava:listenablefuture'
			}
			dependency 'org.apache.xmlgraphics:fop:2.3', {
				exclude 'org.apache.xmlgraphics:batik-extension'
				exclude 'commons-logging:commons-logging'
				exclude 'org.apache.pdfbox:fontbox'
				exclude 'xalan:xalan'
				exclude 'xml-apis:xml-apis'
			}
			dependency 'com.github.akarnokd:rxjava2-swing:0.3.7'
			dependency 'org.beanfabrics:beanfabrics-swing:1.4.3'
			dependency 'org.swinglabs:jxlayer:3.0.4'
			dependency 'com.fifesoft:rsyntaxtextarea:3.0.3'
			dependency 'org.ini4j:ini4j:0.5.4'
			dependency 'com.github.jgraph:jgraphx:4.0.3'
			dependency 'com.github.markusbernhardt:proxy-vole:1.0.5'
			dependency 'org.apache.logging.log4j:log4j-slf4j-impl:2.12.1'
			dependency 'org.apache.logging.log4j:log4j-core:2.12.1'
			dependency 'com.github.zafarkhaja:java-semver:0.9.0'
			dependency 'org.glassfish.jaxb:jaxb-runtime:2.3.2', {
				exclude 'com.sun.xml.fastinfoset:FastInfoset'
				exclude 'org.glassfish.jaxb:txw2'
				exclude 'org.jvnet.staxex:stax-ex'
			}
			dependency 'jakarta.xml.bind:jakarta.xml.bind-api:2.3.2'

			dependency 'junit:junit:4.12'
		}
	}

	if (prj.name in nonJavaProjectsFilter) {
		return
	}

	apply plugin: 'java'
	apply plugin: 'maven-publish'

	publishing {
		publications {
			maven(MavenPublication) {
				from components.java
			}
		}
	}

	compileJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
		options.encoding = 'utf-8'
		options.compilerArgs << "-proc:none"
		options.compilerArgs << "-Xlint:deprecation"
	}

	compileTestJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
		options.encoding = 'utf-8'
	}

	jar {
		manifest {
			attributes(
				"Specification-Title": prj.name,
				"Specification-Version": scmVersion.baseVersion,
				"Implementation-Title": prj.name,
				"Implementation-Version": scmVersion.distVersion,
				"Build-Id": scmVersion.buildId,
				"Build-Time": scmVersion.buildTimestamp,
				"Permissions": "all-permissions"
			)
		}
	}
}
