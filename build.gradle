plugins {
	id 'io.spring.dependency-management' version '1.0.3.RELEASE' apply false
	id 'org.ajoberstar.grgit' version '2.0.1' apply false
}

def nonJavaProjectsFilter = [
	"grafikon-assembly"
]

subprojects { prj ->
	apply plugin: 'org.ajoberstar.grgit'
	apply plugin: 'io.spring.dependency-management'

	group = 'net.parostroj'
	version = '2.1.0-SNAPSHOT'


	ext {
		dirty = !grgit.status().clean
		gitHead = grgit.head()
		buildTimestamp = new Date().format('yyyyMMddHHmm')
		commitTimestamp = Date.from(gitHead.dateTime.toInstant()).format('yyyyMMddHHmm')
		commitId = gitHead.id.substring(0, 12)
		buildId = commitId
		completeVersion = version.endsWith("-SNAPSHOT")
			? "${version.replace('-SNAPSHOT', '')}-dev.${commitTimestamp}+${buildId}${dirty ? '#' : ''}"
			: "${version}+${buildId}${dirty ? '#' : ''}"
	}

	repositories {
		mavenLocal()
		mavenCentral()
	}

	def subs = { 
		substitute module('net.parostroj:grafikon-model') with project(':grafikon-model')
		substitute module('net.parostroj:grafikon-save') with project(':grafikon-save')
		substitute module('net.parostroj:grafikon-ls') with project(':grafikon-ls')
		substitute module('net.parostroj:grafikon-ls4') with project(':grafikon-ls4')
		substitute module('net.parostroj:grafikon-templates') with project(':grafikon-templates')
		substitute module('net.parostroj:grafikon-output2') with project(':grafikon-output2')
		substitute module('net.parostroj:grafikon-gui-components') with project(':grafikon-gui-components')
		substitute module('net.parostroj:grafikon-gui') with project(':grafikon-gui')
		substitute module('net.parostroj:grafikon-start') with project(':grafikon-start')
	}

	configurations.all {
		resolutionStrategy {
			dependencySubstitution subs
		}
	}

	dependencyManagement {
		resolutionStrategy {
			dependencySubstitution subs
		}
		dependencies {
			dependency 'org.jgrapht:jgrapht-core:1.0.1'
			dependency 'org.mvel:mvel2:2.3.2.Final'
			dependency 'org.codehaus.groovy:groovy-all:2.4.12'
			dependency 'org.slf4j:slf4j-api:1.7.25'
			dependency 'org.slf4j:jcl-over-slf4j:1.7.25'
			dependency 'joda-time:joda-time:2.9.9'
			dependency ('com.google.guava:guava:23.2-jre') {
				exclude 'com.google.code.findbugs:jsr305'
				exclude 'com.google.j2objc:j2objc-annotations'
				exclude 'com.google.errorprone:error_prone_annotations'
				exclude 'org.codehaus.mojo:animal-sniffer-annotations'
			}
			dependency ('org.apache.xmlgraphics:fop:2.2') {
				exclude 'org.apache.xmlgraphics:batik-extension'
				exclude 'commons-logging:commons-logging'
				exclude 'org.apache.pdfbox:fontbox'
				exclude 'xalan:xalan'
				exclude 'xml-apis:xml-apis-ext'
			}

			dependency 'org.apache.xmlgraphics:batik-svggen:1.9'
			dependency ('org.apache.xmlgraphics:batik-dom:1.9') {
				exclude 'xalan:xalan'
				exclude 'xml-apis:xml-apis-ext'
			}
			dependency 'com.github.akarnokd:rxjava2-swing:0.2.5'
			dependency 'org.beanfabrics:beanfabrics-swing:1.4.3'
			dependency 'org.swinglabs:jxlayer:3.0.4'
			dependency 'com.fifesoft:rsyntaxtextarea:2.6.1'
			dependency 'org.ini4j:ini4j:0.5.4'
			dependency 'org.tinyjee.jgraphx:jgraphx:3.4.1.3'
			dependency 'com.github.markusbernhardt:proxy-vole:1.0.3'
			dependency 'org.apache.logging.log4j:log4j-slf4j-impl:2.9.1'

			dependency 'junit:junit:4.12'
		}
	}

	if (prj.name in nonJavaProjectsFilter) {
		return
	}

	apply plugin: 'java'

	compileJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
		options.encoding = 'utf-8'
	}

	jar {
		manifest {
			attributes(
				"Implementation-Title": prj.name,
				"Implementation-Version": completeVersion,
				"Build-Id": buildId,
				"Build-Time": buildTimestamp,
				"Permissions": "all-permissions"
			)
		}
	}
}
