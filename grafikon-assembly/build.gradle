plugins {
	id 'base'
}

configurations {
	assembly
}

dependencies {
	assembly project(':grafikon-start')
}

task distribution(type: Zip) {
	archiveBaseName = "grafikon"
	archiveVersion = scmVersion.shortVersion
	destinationDirectory = file("${buildDir}/dist")
	from configurations.assembly
	from file("${projectDir}/src/main/scripts")
	rename "grafikon-start.*", "grafikon.jar"
	doFirst {
		def force = project.hasProperty("forceDistribution")
		if (scmVersion.dirty && !force) {
			throw new GradleException("Creating of distribution from not clean working directory forbidden!")
		}
	}
}

task unzippedDistribution(type: Sync) {
    dependsOn distribution
    from zipTree(distribution.archiveFile)
    into "${buildDir}/unzipped-dist"
}
