plugins {
	id 'base'
}

configurations {
	assembly
}

dependencies {
	assembly project(':grafikon-start')
}

def dist = tasks.register('distribution', Zip) {
	group = "distribution"
	archiveBaseName = "grafikon"
	archiveVersion = scmVersion.shortVersion
	destinationDirectory = file("${buildDir}/dist")
	from configurations.assembly
	from file("${projectDir}/src/main/scripts")
	rename "grafikon-start.*", "grafikon.jar"
	doFirst {
		def force = project.hasProperty("forceDistribution")
		if (scmVersion.dirty && !force) {
			throw new GradleException("Creating of distribution from not clean working directory forbidden!")
		}
	}
}

tasks.register('unzippedDistribution', Sync) {
	group = "distribution"
	dependsOn dist
	from zipTree(dist.get().archiveFile)
	into "${buildDir}/unzipped-dist"
}
